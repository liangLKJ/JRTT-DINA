model{
  for (n in 1:N) {
    for (i in 1:I) {
      for (k in 1:K) {
        w[n, i, k] <- pow(alpha[n, k], Q[i, k])
      } 
  logit(prob[n, i]) <- beta[i]+delta[i] * prod(w[n, i,])
  Y[n, i] ~ dbern(prob[n, i])  
  Timelog[n,i]~ dnorm(zeta[i]-tau[n],den_epsilon[i])
  }
}
  for (n in 1:N) {  
    for (k in 1:K) {
      logit(att_prob[n, k]) <- gamma[k] * theta[n] - lambda[k]
      alpha[n,k] ~ dbern(att_prob[n, k])
    }
  }    
  for (n in 1:N){
    person_parameter[n,1:2] ~ dmnorm(person_mu[1:2],person_den[1:2,1:2])
    theta[n] <- person_parameter[n,1]
    tau[n]  <- person_parameter[n,2]
  }
  for (i in 1:I){
    item_parameter[i,1:3] ~ dmnorm(item_mu[1:3],item_den[1:3,1:3])
    beta[i] <- item_parameter[i,1]
    delta[i] <- item_parameter[i,2]
    zeta[i] <- item_parameter[i,3]
    logit(g[i]) <- beta[i]
    logit(ns[i]) <- beta[i]+delta[i]
    s[i] <- 1-ns[i]
    den_epsilon[i] ~  dgamma(1,1)  
    sigma_epsilon[i] <- 1/den_epsilon[i]
  }
  for (k in 1:K){
    lambda[k] ~ dnorm(0,4)
    gamma[k] ~ dnorm(0,4) T(0, )
  }
  person_mu[1] <- 0
  person_mu[2] <- 0  
  L_theta[1,1] <- 1
  L_theta[2,2]  ~ dgamma(1,1)  
  L_theta[2,1]  ~ dnorm(0,1)
  L_theta[1,2] <- 0
  sigma_theta <- L_theta %*% t(L_theta)
  person_den[1:2,1:2] <- inverse(sigma_theta[1:2,1:2])
  item_mu[1] ~ dnorm(-0.35075,2)
  item_mu[2] ~ dnorm(1.91431,2) T(0, )
  item_mu[3] ~ dnorm(4.33211,2)
  R[1,1] <- 1
  R[2,2] <- 1
  R[3,3] <- 1
  R[1,2] <- 0
  R[1,3] <- 0
  R[2,1] <- 0
  R[2,3] <- 0
  R[3,1] <- 0
  R[3,2] <- 0
  item_den[1:3,1:3] ~ dwish(R[1:3,1:3],3)
  sigma_item[1:3,1:3] <- inverse(item_den[1:3,1:3])
}
